# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st
_pkgname=st
pkgver=0.8.2
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
_patches=('https://st.suckless.org/patches/scrollback/st-scrollback-20190122-3be4cf1.diff'
          'https://st.suckless.org/patches/scrollback/st-scrollback-mouse-0.8.diff'
          'https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-20190131-e23acb9.diff'
          'st-dracula-0.8.2.diff'
          )
# include config.h and any patches you want to have applied here
source=("https://dl.suckless.org/st/${_pkgname}-${pkgver}.tar.gz"
        'config.h'
        ${_patches[@]})
sha1sums=('1093714e46adf3f02f9ae9922c49d8ff63b83128'
          '55a6e62663f8ebe1951353275f53090be7247933'
          '0be8cf5c0098569e4b4d862183014c0a052dc704'
          '46e92d9d3f6fd1e4f08ed99bda16b232a1687407'
          '0743f3736ff18be535e25c0916a89e5eed9d5f4f'
          '0ae1b440a2b09fc366a1399affb8376fdf0f186f')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

#pkgver() {
#	cd "${_pkgname}"
#	git describe --tags |sed 's/-/./g'
#}

prepare() {
	local file
	cd "${_pkgname}-${pkgver}"
	sed '/@tic/d' -i Makefile
	for file in "${source[@]}"; do
		if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
		elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
			msg "Applying patch $(basename ${file})"
			# add all patches present in source array
			patch -Np1 <"$srcdir/$(basename ${file})"
		fi
	done
}

build() {
	cd "${_pkgname}-${pkgver}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
