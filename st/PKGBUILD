# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
_pkgname=st
pkgver=0.7.38.g0ac685f
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
epoch=1
_patches=('https://st.suckless.org/patches/scrollback/st-scrollback-20170329-149c0d3.diff'
          'https://st.suckless.org/patches/scrollback/st-scrollback-mouse-20170427-5a10aca.diff'
          'https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-20170427-5a10aca.diff'
          'https://st.suckless.org/patches/delkey/st-delkey-20160727-308bfbf.diff'
          'https://st.suckless.org/patches/dracula/st-dracula-20170803-7f99032.diff'
          )
# include config.h and any patches you want to have applied here
source=('git://git.suckless.org/st'
        'config.h'
        ${_patches[@]})
sha1sums=('SKIP'
          'SKIP'
          '6a41683a04375f192d014cead227c749c595a721'
          '81b4a93c7bbf6854ec60630a5f7f50268a106a4b'
          'b181427a52dea0f1b5635cf51263a016d4d27cdf'
          '1b3a6708a3644414fa26f07db8b2e1bb719f0381'
          'f263e7d8a490355caad6514a8e053b89cba52145')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
	cd "${_pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	local file
	cd "${_pkgname}"
	sed '/@tic/d' -i Makefile
	for file in "${source[@]}"; do
		if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
		elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
			msg "Applying patch $(basename ${file})"
			# add all patches present in source array
			patch -Np1 <"$srcdir/$(basename ${file})"
		fi
	done
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
