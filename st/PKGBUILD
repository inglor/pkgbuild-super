# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st
_pkgname=st
pkgver=0.8.2.r31.g51e19ea
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
_patches=('https://st.suckless.org/patches/scrollback/st-scrollback-20190331-21367a0.diff'
          'https://st.suckless.org/patches/scrollback/st-scrollback-mouse-20191024-a2c479c.diff'
          'https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-20191024-a2c479c.diff'
          'https://st.suckless.org/patches/dracula/st-dracula-0.8.2.diff'
          )
# include config.h and any patches you want to have applied here
source=("git+https://git.suckless.org/st"
        'config.h'
        ${_patches[@]})
sha256sums=('SKIP'
            '18abdbb1bb4f26e933391bfeb01adf9812833c69d9b52a523a3dbc40db4666de'
            'f974bc0e11bda909a6d6d529d28abde4c97e651107b6a6047f2fab8837010b43'
            '319458d980195d18fa0f81a6898d58f8d046c5ff982ab872d741f54bb60e267d'
            '3e65338b3be0d25af699251fac3a705e1bf09a4e2d368449f2f08c2a25e969da'
            '5eb8e0375fda9373c3b16cabe2879027300e73e48dbd9782e54ffd859e84fb7e')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	local file
	cd "${_pkgname}"
	sed '/@tic/d' -i Makefile
	for file in "${source[@]}"; do
	if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
		elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
			msg "Applying patch $(basename ${file})"
			# add all patches present in source array
			patch -Np1 <"$srcdir/$(basename ${file})"
		fi
	done
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
