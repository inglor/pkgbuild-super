# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st
_pkgname=st
pkgver=0.8.3.r0.g43a395a
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
_patches=('https://st.suckless.org/patches/scrollback/st-scrollback-20200419-72e3f6c.diff')
# include config.h and any patches you want to have applied here
source=("git+https://git.suckless.org/st#tag=0.8.3"
        'config.h'
        ${_patches[@]})
sha256sums=('SKIP'
            '273ea7009e10529d0b3577f6072d66b48a6a770f6b5e406ef02ce047b77ef680'
            '1e41fe17a5ef5a8194eea07422b49d815e2c2bb4d58d84771f793be423005310')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	local file
	cd "${_pkgname}"
	sed '/@tic/d' -i Makefile
	for file in "${source[@]}"; do
	    if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
		elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
			msg "Applying patch $(basename ${file})"
			# add all patches present in source array
			patch -Np1 <"$srcdir/$(basename ${file})"
		fi
	done
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
