# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st
_pkgname=st
pkgver=0.8.1
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
_patches=('https://st.suckless.org/patches/scrollback/st-scrollback-0.8.diff'
          'https://st.suckless.org/patches/scrollback/st-scrollback-mouse-0.8.diff'
          'https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-0.8.diff'
          )
# include config.h and any patches you want to have applied here
source=("https://git.suckless.org/st/snapshot/${_pkgname}-${pkgver}.tar.gz"
        'config.h'
        ${_patches[@]})
sha1sums=('SKIP'
          'SKIP'
          '623f474b62fb08e0b470673bf8ea947747e1af8b'
          '46e92d9d3f6fd1e4f08ed99bda16b232a1687407'
          'd3329413998c5f3feaa7764db36269bf7b3d1334'
          )

provides=("${_pkgname}")
conflicts=("${_pkgname}")

#pkgver() {
#	cd "${_pkgname}"
#	git describe --tags |sed 's/-/./g'
#}

prepare() {
	local file
	cd "${_pkgname}-${pkgver}"
	sed '/@tic/d' -i Makefile
	for file in "${source[@]}"; do
		if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
		elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
			msg "Applying patch $(basename ${file})"
			# add all patches present in source array
			patch -Np1 <"$srcdir/$(basename ${file})"
		fi
	done
}

build() {
	cd "${_pkgname}-${pkgver}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
